# Some sample images generated by the program in svg format
Note: the Turtle Graphic vector emf files exported from the add-in were converted to svg with Inkscape. The background was removed first and then the page size was adjusted to the shape (CTRL+SHIFT+D and "Resize page to drawing or selection")


## Butterfly
![Butterfly](./turtle-graphics-butterfly.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub butterfly()
  Dim WingSize As Single, wingColors As Variant, Size As Long, i As Long
  
  turtle.Reset
  turtle.DrawingMode = ttNoScreenRefresh
  turtle.TurnLeft 45
  
  wingColors = Array(ttred, ttblue, ttmagenta, ttyellow, ttgreen, ttgold)
  With turtle:
    For Size = 100 To 50 Step -10
      
      .FillColor = wingColors(Size / 10 - 5)
      .PenDown
      For i = 1 To 4
        If i > 2 Then
          WingSize = Size * 0.7
        Else
          WingSize = Size
        End If
        .MoveCurved WingSize, WingSize / 4, ttPetalFd
        .MoveCurved -WingSize, -WingSize / 4, ttPetalFd
        .TurnLeft 90
      Next i
      .PenUp
    Next Size
    .PointInDirection 0
    .Turnright 17
    .FillColor = ttInvisible
    .PenDown
    .MoveCurved 100, 95, ttchord1
    .PenUp
    .Move -100
    .TurnLeft 17 * 2
    .PenDown
    .MoveCurved 100, -95, ttchord1
    .PenUp
    .Move -100
    
  End With
End Sub

```
</details>

## Badge
![Badge](./turtle-graphics-badge.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub Badge()
  Dim points As Long, i As Long, length As Long

  points = 6
  length = 100

  With turtle
    .Clear
    .PenUp
    .PointInDirection 0
    .FillType = ttSolid
    .FillColor = ttgold
    .PenColor = ttInvisible
    .Center
    For i = 1 To points
      .Move length
      .Point
      .PenDown
      .Ellipse length / points
      .PenUp
      .Move -length
      .Turnright 360 / points / 2
      .Move length * 0.6
      .Point
      .Move -length * 0.6
      .Turnright 360 / points / 2
    Next i
    .ClosePoints
    .PenColor = ttWhite
    .PenSize = length / points / 2
    .FillColor = ttInvisible
    .PointInDirection 90
    .PenDown
    .Ellipse 2 * length * 0.6 - length / points / 3
    .PenUp
    .FontSize = length / 3
    .FontColor = ttWhite
    .FontName = "Playbill"
    .WriteText "SHERIFF"
    .Group
    .FillColor = ttInvisible
    .PenColor = ttBlack
    .PenSize = 1
  End With

  turtle.GoToXY 20, 20
End Sub  
```
</details>

## Flower with hexagons
![Flower with hexagons](./turtle-graphics-flower1.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub flower1()
  Dim j As Long, i As Long, sides As Long, petals As Long, color As ttcolors

  sides = 6
  petals = 10
  color = ttOrange
  
  With turtle
    .Center
    .Clear
    .FillColor = color
    .PenDown
    For j = 1 To petals
      .Turnright 360 / petals
      For i = 1 To sides
        .Move 300 / sides
        .Turnright 360 / sides
      Next i
    Next j
    .PenUp
    .FillColor = ttInvisible
    .PenDown
  End With
End Sub
```
</details>


## Flower with curved hexagons
![Flower with curved hexagons](./turtle-graphics-flower2.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub flower2()
  Dim j As Long, i As Long, sides As Long, petals As Long, color As ttcolors

  sides = 6
  petals = 10
  color = ttOrange
  
  With turtle
    .Center
    .Clear
    .FillColor = color
    .PenDown
    For j = 1 To petals
      .Turnright 360 / petals
      For i = 1 To sides
        .MoveCurved 300 / sides, 110 / sides, ttaxis
        .Turnright 360 / sides
      Next i
    Next j
    .PenUp
    .FillColor = ttInvisible
    .PenDown
  End With
End Sub
```
</details>



## Sierpinski triangle
![Sierpinski triangle](./turtle-graphics-sierpinski.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub sierpinski(length As Long, depth As Long)
  Dim i As Integer

  With turtle
    If depth = 0 Then
      For i = 0 To 2
        .Move length
        .TurnLeft 120
      Next i
      Exit Sub
    End If
    sierpinski length / 2, depth - 1
    .Move length / 2
    sierpinski length / 2, depth - 1
    .Move -length / 2
    .TurnLeft 60
    .Move length / 2
    .Turnright 60
    sierpinski length / 2, depth - 1
    .TurnLeft 60
    .Move -length / 2
    .Turnright 60
  End With
End Sub


Sub draw_triangle()
  Dim depth As Long
  
  depth = 3

  With turtle
    .Reset
    .DrawingMode = ttNoScreenRefresh
    .FillType = ttSolid
    .FillColor = ttyellow
    .y = .y + 100
    .x = .x - 100
    sierpinski 200, depth
    .PenUp
    .FillColor = ttInvisible
  End With
End Sub
```
</details>
  

## Koch snowflake
![Koch snowflake](./turtle-graphics-koch.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub Koch(depth As Long, length As Single)
  With turtle
    If depth = 1 Then
      .Move length
      Exit Sub
    End If
    Koch depth - 1, length / 3
    .Turnleft 60
    Koch depth - 1, length / 3
    .Turnright 120
    Koch depth - 1, length / 3
    .Turnleft 60
    Koch depth - 1, length / 3
  End With
End Sub


Sub draw_snowflake()
  Dim i As Long, depth As Long
  
  depth = 5
  
  With turtle
    .Reset
    .FillType = ttSolid
    .FillColor = ttyellow
    .PenUp
    .x = .x - 100
    .y = .y - 70
    .PenDown
    For i = 1 To 3
      Koch depth, 200
      .Turnright 120
    Next i
    .PenUp
    .FillColor = ttInvisible
  End With
End Sub



```
</details>


## Polyspiral
![Polyspiral](./turtle-graphics-polyspiral.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
sub Spiral()

  Dim length, angle, d
  Dim c

    d = 1
    
    length = 300
    angle = 89

    
    With turtle
      .Reset
      .PenDown
      .PenSize = 0.5
      .FillColor = ttSkyBlue
      Do While length > d
        .Move length
        .TurnLeft angle
        length = length - d
      Loop
      .PenUp
      .Hide
    End With
    
End Sub


```
</details>


## Colored Polyspiral
This one had to be edited in Inkscape to change the stroke cap to rounded corners, since that feature is unavailable to VBA
![Coloured Polyspiral](./turtle-graphics-polyspiral2.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub Spiral()
  ' Based on https://juliagraphics.github.io/Luxor.jl/v2.2/turtle/
  Dim length, angle, d
  Dim i

    d = 0.75
    
    length = 5
    angle = 89.5

    
    With turtle
      .CanvasColor = ttBlack
      .Reset
      .DrawingMode = ttNoScreenRefresh
      .Hide
      .PenSize = 1
      .PenColor = ttCyan
      For i = 1 To 400
        .PenDown
        .Move length
        .Turnright angle
        length = length + d
        .PenHueShift 1
        .PenUp
      Next i
      .Hide
    End With
    
End Sub


```
</details>


## Concentric mosaic

![Concentric mosaic](./turtle-graphics-concentric-mosaic.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub concentric()
  Dim circ As Long, segments As Long, diameter As Long, increment As Long, segment As Long
  Dim initialSegment As Single, angle As Single, newAngle As Single, levels As Long
  
  segments = 32
  diameter = 225
  increment = 20
  levels = 11
  
  With turtle
    .Reset
    .FillColor = ttCyan
    For circ = 1 To levels
      
      initialSegment = Rnd() * 240 / segments - 120 / segments
      angle = initialSegment
      If circ <> levels Then
        .FillColor = RGB(Rnd() * 255, Rnd() * 255, Rnd() * 255)
      Else
        .FillColor = ttwhite
        segments = 1
      End If
      For segment = 1 To segments
        If segment <> segments Then
          newAngle = segment * 360 / segments + Rnd() * 240 / segments - 120 / segments
        Else
          newAngle = initialSegment
        End If
        If segments <> 1 Then
          .Arc diameter, diameter, angle, newAngle, ttsector
        Else
          .Arc diameter, diameter, angle, newAngle, TTARC
        End If
        angle = newAngle
        .FillHueShift 10
      Next segment
      diameter = diameter - increment
      segments = segments - 3
    Next circ
  
  End With
End Sub


```
</details>

## Batik Flower

![Batik flower](./turtle-graphics-batik-flower.svg?raw=true)

<details>
  <summary>Click to show code</summary>
  
```VBA
Sub batik_flower()
  Dim sides As Long, color As ttColors, i As Long
  Dim length As Single, R As Single, interior_sides As Long
  Dim interior_length As Single
  
  sides = 8
  R = 50
  interior_sides = 8
  
  
  interior_length = R / 1.5
  With turtle
    .Reset
    .PenUp
    .PenColor = ttInvisible
    'Pistils
    .TurnLeft 90
    .Move R
    .TurnRight 45
    .FillColor = RGB(0, 128, 0)
    .Pendown
    length = R * (2 * Sin([Pi()] / 4))
    For i = 1 To 4
      .Movecurved R / 2, 0, ttLine
      .TurnRight 90
      .Movecurved length, length, ttCusp
      .TurnRight 90
      .Movecurved R / 2, 0, ttLine
      .TurnLeft 90
    Next i
    .PenUp
    
    'Petals
    length = R * (2 * Sin([Pi()] / sides))
    .PointInDirection 90
    .Center
    .TurnLeft 360 / (2 * sides)
    .Move R
    .TurnRight 90 + 360 / (2 * sides)
    .FillColor = RGB(191, 191, 0)
    .Pendown
    For i = 1 To sides
     .Movecurved length, length / 1.5, ttHalfEllipse
     .TurnRight 360 / sides
    Next i
    .PenUp
  
    
    .TurnLeft 90 + 360 / (2 * sides)
    .Move -R


    .FillColor = ttBlack
    .TurnLeft 360 / 32
    star 16, R * 1.1
    .TurnRight 360 / 32
    .FillColor = vbWhite
   
    For i = 1 To interior_sides
        .Pendown
        .Movecurved interior_length, interior_length / 7, ttPetalfd
        .Movecurved -interior_length, interior_length / 7, ttPetalbk
        .TurnLeft 360 / interior_sides
        .PenUp
    Next i
    .Pendown
    .FillColor = ttwhite
    .PenColor = ttBlack
    .PenSize = 3
    .ellipse interior_length / 2.5
    .PenUp
    .Hide
  End With
  
  
End Sub

Sub star(ByVal points As Long, ByVal length As Single)
  Dim i As Long
  With turtle
    For i = 1 To points
        .Move length
        .Point
        .Move -length
        .TurnRight 360 / points / 2
        .Move length * 0.6
        .Point
        .Move -length * 0.6
        .TurnRight 360 / points / 2
    Next i
    .ClosePoints
  End With
End Sub

```
</details>

